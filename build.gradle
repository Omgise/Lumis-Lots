buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
            changing = true
        }
    }
}

apply plugin: 'forge'

version = "1.6.0"
group = "com.lumi.lots"
archivesBaseName = "Lumis-Lots"

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "run"
}

tasks.withType(JavaExec) {
    if (it.name == 'runClient' || it.name == 'runServer') {
        it.args '--tweakClass', 'org.spongepowered.asm.launch.MixinTweaker'
    }
}

repositories {
    mavenCentral()
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

dependencies {
    implementation ("com.github.LegacyModdingMC.UniMixins:unimixins-all-1.7.10:0.1.23")
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.compilerArgs += [
        "-AreobfSrgFile=${tasks.reobf.srg}",
        "-AoutSrgFile=${buildDir}/tmp/mixins/mixins.srg",
        "-AoutRefMapFile=${buildDir}/resources/main/mixins.lumislots.refmap.json"
    ]
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': "1.7.10"
    }
}

jar {
    manifest {
        attributes([
                'MixinConfigs': 'mixins.lumislots.json',
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'FMLCorePluginContainsFMLMod': 'true',
                'ForceLoadAsMod': 'true'
        ])
    }
}

/* export JAVA_HOME=$(/usr/libexec/java_home -v 1.8) */